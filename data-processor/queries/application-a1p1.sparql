PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ASCTB-TEMP: <https://purl.org/ccf/ASCTB-TEMP_>
PREFIX CL: <http://purl.obolibrary.org/obo/CL_>
PREFIX FMA: <http://purl.org/sig/ont/fma/fma>
PREFIX UBERON: <http://purl.obolibrary.org/obo/UBERON_>
PREFIX ccf3d: <http://purl.org/ccf/latest/ccf.owl#>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX ctpop: <https://cns-iu.github.io/hra-cell-type-populations-supporting-information/data/enriched_rui_locations.jsonld#>
PREFIX dc: <http://purl.org/dc/terms/>
PREFIX hubmap: <https://entity.api.hubmapconsortium.org/entities/>
PREFIX rui: <http://purl.org/ccf/1.5/>


SELECT ?sample ?dataset ?as ?similarity ?as_in_collisions
WHERE {
  ?dataset a ccf:Dataset .
  GRAPH ctpop:graph {
    ?sample ccf:generates_dataset ?dataset .
    ?sample ccf:has_registration_location ?rui_location .
  }

  {
    GRAPH ctpop:similarities {
      [] ccf:cell_source_a ?dataset ;
         ccf:cell_source_b ?as ;
         ccf:similarity ?similarity .

      FILTER (
        STRSTARTS(STR(?as), 'http://purl.obolibrary.org/obo/') ||
        STRSTARTS(STR(?as), 'http://purl.org/sig/ont/fma/fma')
      )
    }
  } UNION {
    GRAPH ctpop:similarities {
      [] ccf:cell_source_a ?as ;
         ccf:cell_source_b ?dataset ;
         ccf:similarity ?similarity .
      
      FILTER (
        STRSTARTS(STR(?as), 'http://purl.obolibrary.org/obo/') ||
        STRSTARTS(STR(?as), 'http://purl.org/sig/ont/fma/fma')
      )
    }
  }

  BIND (EXISTS {
    GRAPH ctpop:graph {
      ?rui_location ccf:has_collision_summary [
        ccf:has_collision_item [
          ccf:as_id ?as
        ]
      ]
    }
  } as ?as_in_collisions)
}
ORDER BY ?sample
