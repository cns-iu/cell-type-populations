PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ASCTB-TEMP: <https://purl.org/ccf/ASCTB-TEMP_>
PREFIX CL: <http://purl.obolibrary.org/obo/CL_>
PREFIX FMA: <http://purl.org/sig/ont/fma/fma>
PREFIX UBERON: <http://purl.obolibrary.org/obo/UBERON_>
PREFIX ccf3d: <http://purl.org/ccf/latest/ccf.owl#>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX ctpop: <https://cns-iu.github.io/hra-cell-type-populations-supporting-information/data/enriched_rui_locations.jsonld#>
PREFIX dc: <http://purl.org/dc/terms/>
PREFIX hubmap: <https://entity.api.hubmapconsortium.org/entities/>
PREFIX rui: <http://purl.org/ccf/1.5/>


SELECT ?sample ?rui_location ?dataset ?modality (?rui as ?corridor) ?similarity ?distance 
WHERE {
  GRAPH ctpop:graph {
    ?sample ccf:generates_dataset ?dataset .
    ?sample ccf:has_registration_location ?rui_location .
    [] ccf:has_registration_location ?rui .

    ?dataset ccf:has_cell_summary [
      ccf:modality ?modality ;
    ] .

    FILTER (?rui != ?rui_location)
  }

  {
    GRAPH ctpop:distances {
      [] a ccf:SpatialEntityDistance;
        ccf:entity_a ?rui ;
        ccf:entity_b ?rui_location ;
        ccf:distance ?distance .
    }
  } UNION {
    GRAPH ctpop:distances {
      [] a ccf:SpatialEntityDistance;
        ccf:entity_a ?rui_location ;
        ccf:entity_b ?rui ;
        ccf:distance ?distance .
    }
  }

  {
    GRAPH ctpop:similarities {
      [] ccf:cell_source_a ?dataset ;
         ccf:cell_source_b ?rui ;
         ccf:similarity ?similarity .
    }
  } UNION {
    GRAPH ctpop:similarities {
      [] ccf:cell_source_a ?rui ;
         ccf:cell_source_b ?dataset ;
         ccf:similarity ?similarity .
    }
  }
}
ORDER BY ?sample DESC(?similarity)
