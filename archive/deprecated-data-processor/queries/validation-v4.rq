PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ASCTB-TEMP: <https://purl.org/ccf/ASCTB-TEMP_>
PREFIX CL: <http://purl.obolibrary.org/obo/CL_>
PREFIX FMA: <http://purl.org/sig/ont/fma/fma>
PREFIX UBERON: <http://purl.obolibrary.org/obo/UBERON_>
PREFIX ccf3d: <http://purl.org/ccf/latest/ccf.owl#>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX ctpop-graph: <https://purl.humanatlas.io/graph/ctpop>
PREFIX ctpop: <https://purl.humanatlas.io/graph/ctpop#>
PREFIX dc: <http://purl.org/dc/terms/>
PREFIX hubmap: <https://entity.api.hubmapconsortium.org/entities/>
PREFIX rui: <http://purl.org/ccf/1.5/>

SELECT
  ?modality
  (COUNT(DISTINCT(?asctb)) as ?asctb_only) 
  (COUNT(DISTINCT(?both)) as ?both) 
  (COUNT(DISTINCT(?exp)) as ?exp_only)
WHERE {
  
  # Get CTs only in ASCT+B
  {
    SELECT DISTINCT ?modality ?asctb {
      GRAPH <https://purl.org/ccf/releases/2.2.1/ccf.owl> {
        ?asctb ccf:ccf_asctb_type ?type .
      }
      GRAPH ctpop-graph: {
        [] ccf:has_cell_summary [
            ccf:modality ?modality ;
        ]
      }
      FILTER (?type = 'CT')
      FILTER NOT EXISTS {
        GRAPH ctpop-graph: {
          [] ccf:has_cell_summary [
            ccf:modality ?modality ;
            ccf:has_cell_summary_row [
              ccf:cell_id ?asctb ;
            ]
          ] .
        }
      }
    }
  }

  # Get CTs only in Experimental Data
  {
    SELECT DISTINCT ?modality ?exp {
      GRAPH ctpop-graph: {
        [] ccf:has_cell_summary [
          ccf:modality ?modality ;
          ccf:has_cell_summary_row [
            ccf:cell_id ?exp ;
          ]
        ] .
      }
      FILTER NOT EXISTS {
        GRAPH <https://purl.org/ccf/releases/2.2.1/ccf.owl>{
          ?exp ccf:ccf_asctb_type ?type .
        }
        FILTER (?type = 'CT')
      }
    }
  }

  # Get CTs in Both
  {
    SELECT DISTINCT ?modality ?both {
      GRAPH <https://purl.org/ccf/releases/2.2.1/ccf.owl> {
        ?both ccf:ccf_asctb_type ?type .
      }
      GRAPH ctpop-graph: {
        [] ccf:has_cell_summary [
            ccf:modality ?modality ;
        ]
      }
      FILTER (?type = 'CT')
      FILTER EXISTS {
        SELECT ?modality ?both {
          GRAPH ctpop-graph: {
            [] ccf:has_cell_summary [
              ccf:modality ?modality ;
              ccf:has_cell_summary_row [
                ccf:cell_id ?both ;
              ]
            ] .
          }
        }
      }
    }
  }
}
GROUP BY ?modality